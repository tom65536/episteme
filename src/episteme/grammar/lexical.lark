#
# Lexical symbols for EPISTEME.
#

ID_START: /[\p{L}\p{Nl}\p{Sc}]/
ID_MEDIAL: /[\p{Pc}]/ | "_"
ID_CONT: /[\p{L}\p{Mn}\p{Mc}\p{Nl}\p{Nd}\p{Sc}]/
OPERATORS: /[[\p{Sm}]--[+<>=|]]/
SPACE: /[\p{Zs}]/ | "\t"
DIGIT: /[\p{Nd}]/

# Po: !  " # % & ' * , .  / : ; ?  @ \
# Sm: + < = >  | ~
# Sk: ^ ` _

STRING_FULL: /"[^"`]*"/
STRING_LEFT: /"[^"`]*`/
STRING_MID: /`[^"`]*`/
STRING_RIGHT: /`[^"`]*"/

DIGITS: DIGIT+ (ID_MEDIAL DIGIT+)*
MAGNITUDE: "h" | "k" | "M" | "G" | "T" | "P" | "X" | "Z" | "Y" | "R" | "Q"
FRACM: "d" | "c" | "m" | "u" | "n" | "p" | "f" | "a" | "z" | "y" | "r" | "q"
RADIX: DIGIT [DIGIT]
XDIGIT: DIGIT | "A".."Z" | "a".."z"
XDIGITS: XDIGIT+ (ID_MEDIAL XDIGIT+)*

DECIMAL_LITERAL: DIGITS [MAGNITUDE]
RADICAL_LITERAL: RADIX "$" XDIGITS
FLOAT_LITERAL: DIGITS "." DIGITS ["E"i ["+"|"-"] DIGIT+][MAGNITUDE|FRACM]

PLUS: "+"
MINUS: "-"
AST.2: "*"
DIV: "/"
IDIV: "//"
MOD: "mod"i | "%"
DIVMOD: "/%"
POW: "^"
AND: "and"i
OR: "or"i
XOR: "xor"i
NOT: "not"i
GT: ">"
LT: "<"
GE: ">="
LE: "<="
NE: "<>"
IN: "in"i
EQ: "=="
ASSIGN: "="
PIPE_L: _NEWLINE? "|>"
PIPE_R: _NEWLINE? "<|"
COMP_L: ">>"
COMP_R: "<<"

ALIAS: "alias"i
AS: "as"i
ELSE: "else"i
IF: "if"i
OF: "of"i
PRIVATE: "private"i
USE: "use"i

IDENTIFIER: ID_START (ID_MEDIAL? ID_CONT)*

OP_NAME.3: "\\" OPERATORS
    | "\\" ( PLUS | MINUS | AST | DIV | IDIV | MOD )
    | "\\" ( DIVMOD | POW | AND | OR | XOR | NOT | GT | LT | GE | LE )
    | "\\" ( NE | IN | EQ )

OPI: "\\" IDENTIFIER
STRIDENT: "\\" STRING_FULL
STRESC: "\\" (DECIMAL_LITERAL|RADICAL_LITERAL)

ANONYMOUS: "_" (ID_CONT (ID_MEDIAL? ID_CONT)*)?

COMMENT: /[#\uFE5F\uFF03][^\n\r]*/

_NEWLINE: ( /\r?\n/ (SPACE)* | COMMENT )+
